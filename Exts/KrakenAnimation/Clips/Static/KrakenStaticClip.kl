require Math;

object KrakenStaticClip : KrakenClip {
  String names[];
  UInt32 nameToId[String];
  UInt32 types[];
  Xfo values[];

  KrakenClipLookup lookup;
};

function KrakenStaticClip() {
  this.lookup = null;
}

function KrakenStaticClip.setFloat32!(String name, Float32 value) {
  UInt32 id = this.nameToId.get(name, this.values.size());
  if(id == this.values.size()) {
    this.nameToId[name] = this.values.size();
    this.names.push(name);
    this.values.push(Xfo(Vec3(value, 0.0, 0.0)));
    this.types.push(KrakenClipChannel_Float32);
  } else {
    this.values[id].tr.x = value;
    this.types[id] = KrakenClipChannel_Float32;
  }
  this.lookup = null;
}

function KrakenStaticClip.setXfo!(String name, Xfo value) {
  UInt32 id = this.nameToId.get(name, this.values.size());
  if(id == this.values.size()) {
    this.nameToId[name] = this.values.size();
    this.names.push(name);
    this.values.push(value);
    this.types.push(KrakenClipChannel_Xfo);
  } else {
    this.values[id] = value;
    this.types[id] = KrakenClipChannel_Xfo;
  }
  this.lookup = null;
}

UInt32 KrakenStaticClip.getChannelCount() {
  return this.values.size();
}

String KrakenStaticClip.getChannelName(UInt32 id) {
  return this.names[id];
}

UInt32 KrakenStaticClip.getChannelType(UInt32 id) {
  return this.types[id];
}

Float32 KrakenStaticClip.evaluateFloat32!(UInt32 id, KrakenClipContext context) {
  return this.values[id].tr.x;
}

Xfo KrakenStaticClip.evaluateXfo!(UInt32 id, KrakenClipContext context) {
  return this.values[id];
}

function KrakenStaticClip.apply(io KrakenKLRig rig, KrakenClipContext context, Float32 factor) {
  KrakenStaticClip clip = this;
  if(clip.lookup == null)
    clip.lookup = KrakenClipLookup(rig, clip);
  clip.lookup.applyClip(rig, clip, context, factor);
}

function JSONDictValue KrakenStaticClip.saveToJson!() {
  JSONArrayValue values();
  for(Size i=0;i<this.types.size();i++) {
    JSONDictValue value();
    value.setString('name', this.names[i]);
    value.setInteger('type', this.types[i]);
    if(this.types[i] == KrakenClipChannel_Float32) {
      value.setScalar('value', this.values[i].tr.x);
    } else if(this.types[i] == KrakenClipChannel_Xfo) {
      value.setScalar('tr.x', this.values[i].tr.x);
      value.setScalar('tr.y', this.values[i].tr.y);
      value.setScalar('tr.z', this.values[i].tr.z);
      value.setScalar('ori.v.x', this.values[i].ori.v.x);
      value.setScalar('ori.v.y', this.values[i].ori.v.y);
      value.setScalar('ori.v.z', this.values[i].ori.v.z);
      value.setScalar('ori.w', this.values[i].ori.w);
      value.setScalar('sc.x', this.values[i].sc.x);
      value.setScalar('sc.y', this.values[i].sc.y);
      value.setScalar('sc.z', this.values[i].sc.z);
    }
    values.add(value);
  }
  JSONDictValue content();
  content.set('values', values);
  return content;
}

function KrakenStaticClip.loadFromJson!(JSONDictValue json) {
  this.names.resize(0);
  this.nameToId.clear();
  this.types.resize(0);
  this.values.resize(0);

  JSONArrayValue values = json.get('values');
  for(Size i=0;i<values.size();i++) {
    JSONDictValue value = values.get(i);
    String name = value.getString('name');
    Integer type = value.getInteger('type');
    if(type == KrakenClipChannel_Float32)
      this.setFloat32(name, value.getScalar('value'));
    else {
      Xfo xfo;
      xfo.tr.x = value.getScalar('tr.x');
      xfo.tr.y = value.getScalar('tr.y');
      xfo.tr.z = value.getScalar('tr.z');
      xfo.ori.v.x = value.getScalar('ori.v.x');
      xfo.ori.v.y = value.getScalar('ori.v.y');
      xfo.ori.v.z = value.getScalar('ori.v.z');
      xfo.ori.w = value.getScalar('ori.w');
      xfo.sc.x = value.getScalar('sc.x');
      xfo.sc.y = value.getScalar('sc.y');
      xfo.sc.z = value.getScalar('sc.z');
      this.setXfo(name, xfo);
    }
  }
}
